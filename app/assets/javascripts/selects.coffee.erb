$(document).on 'turbolinks:load', ->

  templates =
    child:   (item) -> templates.default(item, icon: 'child', subtitle: item.age)
    process: (item) -> templates.default(item, icon: 'process')
    person:  (item) -> templates.default(item, icon: 'person', subtitle: item.email or item.phone)
    family:  (item) -> templates.default(item, icon: 'family', subtitle: item.members?.join(', '))
    team:    (item) -> templates.default(item, icon: 'users', subtitle: item.members?.join(', '))

    default: (item, defaults={}) ->
      # Set up defaults
      item[key] or= val for own key, val of defaults
      item.icon = iconNameFor(item.icon) if item.icon

      # Render handlebars template
      HandlebarsTemplates.select_result(item)


  # More beautiful looking selects with <select data-selectpicker></select>
  $('[data-selectpicker] + .select2').remove()
  $('[data-selectpicker]').removeClass('select2-hidden-accessible').each ->
    do ($el = $(@)) ->
      options =
        theme: 'bootstrap'
        escapeMarkup: (markup) -> markup
        templateResult: templates[$el.data('template') or 'default']
        templateSelection: templates[$el.data('template') or 'default']

      if $el.data('ajax')
        $.extend options,
          ajax:
            url: $el.data('ajax')
            dataType: 'json'
            delay: 250
            data: (params) ->
              q: params.term
              page: params.page
            processResults: (data, params) ->
              if !params.page and $el.data('new')
                data.unshift({ id: 'New', icon: 'plus', bold: true, text: $el.data('new') })
              results: data
              pagination:
                more: data.length >= <%= ApplicationController::PAGE_SIZE %>

      $el.select2 options
