$(document).on 'turbolinks:load', ->

  templates =
    child:   (item) -> templates.default(item, icon: 'child')
    process: (item) -> templates.default(item, icon: 'process')
    person:  (item) -> templates.default(item, icon: 'person')
    family:  (item) -> templates.default(item, icon: 'family')
    team:    (item) -> templates.default(item, icon: 'users')

    default: (item, defaults={}) ->
      return item.text if item.loading

      # Set up defaults
      item[key] or= val for own key, val of defaults
      item.icon = iconNameFor(item.icon) if item.icon

      if item.icon and item.color
        """<div class="fa-container fa-container-colored">
          <i class="fa fa-#{item.icon}" style="background-color: ##{item.color};"></i>
          #{item.text}
        </div>"""
      else if item.icon
        """<div class="fa-container">
          <i class="fa fa-#{item.icon}"></i>
          #{item.text}
        </div>"""
      else
        item.text


  # More beautiful looking selects with <select data-selectpicker></select>
  $('[data-selectpicker] + .select2').remove()
  $('[data-selectpicker]').removeClass('select2-hidden-accessible').each ->
    do ($el = $(@)) ->
      options =
        theme: 'bootstrap'
        escapeMarkup: (markup) -> markup
        templateResult: templates[$el.data('template') or 'default']
        templateSelection: templates[$el.data('template') or 'default']

      if $el.data('ajax')
        $.extend options,
          ajax:
            url: $el.data('ajax')
            dataType: 'json'
            delay: 250
            data: (params) ->
              q: params.term
              page: params.page
            processResults: (data, params) ->
              data.unshift({ id: 'New', icon: 'plus', text: "<strong>#{$el.data('new')}</strong>" }) if !params.page and $el.data('new')
              results: data
              pagination:
                more: data.length >= <%= ApplicationController::PAGE_SIZE %>

      $el.select2 options
